buildscript {
    repositories {
        maven { url "http://repo1.maven.org/maven2/" }
        maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:1.5.2")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'net.serenity-bdd.aggregator'

repositories {
    mavenLocal()
    maven { url "http://artifactory.reform.hmcts.net/artifactory/libs-release" }
    jcenter()
}

gradle.startParameter.continueOnFailure=true

check.dependsOn aggregate

group = 'uk.gov.hmcts.reform.divorce'
version = '0.0.3-SNAPSHOT'
sourceCompatibility = 1.8

def versions = [
    commonsLang3: '3.0',
    lombok: '1.16.16',
    serenity: '1.5.2',
    serenityCucumber: '1.1.3'
]

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    testCompile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '5.1'
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.3'

    testCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber

    testCompile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    testCompile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.8'
    testCompile group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testCompile group: 'org.skyscreamer', name:'jsonassert', version: '1.2.3'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    testCompile group: 'uk.gov.hmcts.reform', name: 'java-logging', version:'2.0.2'
    testCompile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '0.6.0'
    testCompile project(':test-support')
}

// Hook into the gradle processTestResources task to execute the copyPropsFile custom task
processTestResources{
    doFirst{
        if(rootProject.ext.has('tags')) {
            println "JUnit tags set to: $tags"

            ant.propertyfile(file: "./serenity.properties") {
                entry(key: "tags", value: rootProject.tags)
            }
        }
    }
}

task developAddRelaseSuffix() {
    version = "${version}-SNAPSHOT"
}

test.onlyIf { rootProject.ext.has('automatedTest') && rootProject.automatedTest == true }

test {
    testLogging {
        events "passed", "skipped", "failed"
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\n------------------"
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                println "------------------\n"
            }
        }
    }
}
